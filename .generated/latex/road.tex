\documentclass[a4paper]{article}
\usepackage{longtable}
\usepackage[color]{vdmlisting}
\usepackage{fullpage}
\usepackage{hyperref}
\begin{document}
\title{}
\author{}
\begin{vdm_al}
class Road is subclass of GLOBAL

values
    private st_spacing : real0 = 10.0;

instance variables
    -- public start_position : Position;
    -- public end_position : Position;
    private id : RoadID;
    private cars : set of Car := {};
    private street_lamps : set of StreetLamp;
    private sensors : set of Sensor;

    private length : real0;

operations
    public Road : RoadID * Position * Position ==> Road
    Road(p_id, p_position1, p_position2) == (
        id := p_id;
        -- make road between two positions
        length := p_position1.distance_to(p_position2);
        -- equally space street lamps along the road segment
        let st_positions = {
            distance * st_spacing - (st_spacing / 2)
            | distance in set {1,...,length / st_spacing}
        } in (
            street_lamps := {
                new StreetLamp(id, st_position)
                | st_position in set st_positions
            };
            sensors := {
                new Sensor(self, st_position)
                | st_position in set st_positions
            }
        );
    )
    post length > 0;

    pure public get_id : () ==> RoadID
    get_id() == return id;

    pure public get_length : () ==> real0
    get_length() == return length;

    pure public get_cars : () ==> set of Car
    get_cars() == return cars;

    pure public get_street_lamps : () ==> set of StreetLamp
    get_street_lamps() == return street_lamps;

    pure public get_sensors : () ==> set of Sensor
    get_sensors() == return sensors;

    public add_car : Car ==> ()
    add_car(p_car) == (
        p_car.set_road_id(id);
        cars := cars union {p_car};
    );
    
    public remove_car : Car ==> ()
    remove_car(p_car) == cars := cars \ {p_car};

end Road
\end{vdm_al}
\bigskip
\begin{longtable}{|l|r|r|}
\hline
Function or operation & Coverage & Calls \\
\hline
\hline
Road & 100.0\% & 1 \\
\hline
Road & 100.0\% & 14 \\
\hline
add\_car & 100.0\% & 178 \\
\hline
get\_cars & 100.0\% & 117014 \\
\hline
get\_id & 100.0\% & 604012 \\
\hline
get\_length & 100.0\% & 15739 \\
\hline
get\_sensors & 100.0\% & 91800 \\
\hline
get\_street\_lamps & 100.0\% & 25200 \\
\hline
remove\_car & 100.0\% & 79 \\
\hline
\hline
road.vdmpp & 100.0\% & 854037 \\
\hline
\end{longtable}
\end{document}
