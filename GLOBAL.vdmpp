class GLOBAL

types
    public real0 = real
    inv r == r >= 0;

    public real01 = real
    inv r == r >= 0 and r <= 1;

    public Time = real0;
    public String = seq of char;
    public StreetLampState = <ON> | <OFF>;
    public RoadID = nat1;
    public IntersectionID = nat1;
    public Edge :: from_intersection : IntersectionID
                   to_intersection : IntersectionID;
    public RoadGraph = map RoadID to Edge;
    public IntersectionGraph = map IntersectionID to set of RoadID;

functions
    -- polymorphic function to sum a sequence
    public sum_seq[@T] : seq of @T * (@T * @T -> @T) +> @T
    sum_seq(p_seq, p_operator) ==
        if len p_seq = 0 then
            0
        else
            p_operator(hd p_seq, sum_seq[@T](tl p_seq, p_operator))
    measure len p_seq;

    -- function to add two reals
    -- can be used as the operator in sum_seq[real]
    public add_reals : real * real +> real
    add_reals(a, b) == a + b;

    -- function to add two real0s
    -- can be used as the operator in sum_seq[real0]
    public add_real0s : real0 * real0 +> real0
    add_real0s(a, b) == a + b
    post RESULT >= 0;

    -- function to add two ints
    -- can be used as the operator in sum_seq[int]
    public add_ints : int * int +> int
    add_ints(a, b) == a + b;

    -- sum a sequence of reals
    public sum_seq_reals : seq of real +> real
    sum_seq_reals(p_seq) == sum_seq[real](p_seq, add_reals);

    -- sum a sequence of real0s
    public sum_seq_real0s : seq of real0 +> real0
    sum_seq_real0s(p_seq) == sum_seq[real0](p_seq, add_real0s)
    post RESULT >= 0;

    -- sum a sequence of ints
    public sum_seq_ints : seq of int +> int
    sum_seq_ints(p_seq) == sum_seq[int](p_seq, add_ints);

end GLOBAL