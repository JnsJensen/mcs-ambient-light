class Sensor is subclass of GLOBAL

values
    private static range : real = 20.0; -- range of the sensor in meters

instance variables
    private cars : set of Car := {};
    private road : Road; -- reference to the road it is part of
    private position : real; -- position along the road in meters

operations
    public Sensor : Road * real ==> Sensor
    Sensor(p_road, p_position) == (
        position := p_position;
        road := p_road;
    );

    -- update the sensor with the cars on the road that are in range
    public sense : () ==> ()
    sense() ==
        let all_cars_on_road = road.get_cars() in
            cars := {
                car
                | car in set all_cars_on_road
                & abs(car.get_distance() - position) < range
            };

    pure public get_cars : () ==> set of Car
    get_cars() == return cars;

    pure public get_position : () ==> real
    get_position() == return position;

end Sensor