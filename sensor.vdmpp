class Sensor is subclass of GLOBAL

values
    private static range : real = 50.0; -- Range of the sensor in meters

instance variables
    private cars : set of Car := {};
    private road : RoadSegment; -- Reference to the road it is part of
    private road_id : RoadID;
    private position : real; -- position along the road in meters

operations
    public Sensor : RoadSegment * real ==> Sensor
    Sensor(p_road, p_position) == (
        position := p_position;
        road := p_road;
        road_id := p_road.get_id();
    );

    -- update the sensor with the cars on the road that are in range
    -- attention: maybe combine sense and get cars?
    public sense : () ==> ()
    sense() == (
        let all_cars_on_road = road.get_cars() in
            cars := { car | car in set all_cars_on_road & abs(car.get_progress() - position) < range }
    );

    pure public get_cars : () ==> set of Car
    get_cars() == return cars;

    pure public get_position : () ==> real
    get_position() == return position;

end Sensor