class Intersection is subclass of GLOBAL

instance variables
    private id : IntersectionID;
    private position : Position;
    private connections : set of Road;

operations
    public Intersection : IntersectionID * Position * set of Road ==> Intersection
    Intersection(p_id, p_position, p_roads) == (
        id := p_id;
        connections := p_roads;
        position := p_position;
    );

    pure public get_id : () ==> IntersectionID
    get_id() == return id;

    -- chooses a random road between its connections,
    -- excluding the road from which the car came
    public next_road : Road ==> [Road]
    next_road(p_from_road) == 
        let available_connections = connections \ {p_from_road} in
        if available_connections = {} then
            return nil
        else
            let connections_seq = VDMUtil`set2seq[Road](available_connections) in
            let rand_idx = MATH`rand(len connections_seq) + 1 in
            return connections_seq(rand_idx)
    pre p_from_road in set connections
    post RESULT in set connections \ {p_from_road} or RESULT = nil;

end Intersection